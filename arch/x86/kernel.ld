/* the entry point is _start */
ENTRY(_start)

SECTIONS
{
	/* load the kernel at 0x100000 */
	. = 0X100000;

	/* put the multiboot header first, then the .text section*/
	.text BLOCK(4K) :
	{
		*(.multiboot)
		. = ALIGN(4K);
		__begin_kernel = .;
		*(.text)
	}

	/* read-only data. */
	.rodata BLOCK(4K) : ALIGN(4K)
	{
		*(.rodata)
	}

	/* read-write data (initialized) */
	.data BLOCK(4K) : ALIGN(4K)
	{
		*(.data)
	}

	/* read-write data (uninitialized) and stack */
	.bss BLOCK(4K) : ALIGN(4K)
	{
		*(COMMON)
		*(.bss)
		*(.bootstrap_stack)
		__end_kernel = .;
	}

	/* discard other sections generated by the compiler */
	/DISCARD/ :
	{
		*(.note*)
		*(.indent)
		*(.comment)
		*(.stab)
		*(.stabstr)
	}
}
